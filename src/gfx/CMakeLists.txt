# LCMS2 support
if (${LCMS2_FOUND})
  add_definitions(-DHAVE_LCMS2_H=1)
endif()

include_directories(
  ${CMAKE_SOURCE_DIR}/src
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_BINARY_DIR}/src
  ${Boost_INCLUDE_DIRS}
  ${LCMS2_INCLUDE_DIR}
  )

# Normal C++ source files
SET(GFX_SRCS
gfx.cpp
AbstractColor.cpp
SolidColor.cpp
GradientColor.cpp
NamedColor.cpp
MolColorRef.cpp
DisplayContext.cpp
TextRenderManager.cpp
ColorTable.cpp
StrColorFormat.cpp
Mesh.cpp
Material.cpp
PixelBuffer.cpp
ColCompiler.cpp
DrawElem.cpp
LabelCacheImpl.cpp
SphereSet.cpp
CmsXform.cpp
ColProfMgr.cpp
Hittest.cpp
)

#####
# CueMol interface definitions for MCWRAPGEN
SET(GFX_MCWG_QIFS
AbstractColor.qif
Color.qif
GradientColor.qif
NamedColor.qif
MolColorRef.qif
TextImgBuf.qif
)

#####
# module loader generated by MCWG
SET(GFX_MODLDR_SRC gfx.moddef)

#####
# Color parser
BISON_TARGET(gfx_color_parser
  color_parser.yxx
  ${CMAKE_CURRENT_BINARY_DIR}/color_parser.cpp
  COMPILE_FLAGS "-p col")
FLEX_TARGET(gfx_color_scanner
  color_scanner.lxx
  ${CMAKE_CURRENT_BINARY_DIR}/color_scanner.cpp
  COMPILE_FLAGS "-P col")
ADD_FLEX_BISON_DEPENDENCY(gfx_color_scanner gfx_color_parser)

# message(STATUS "BISON_gfx_color_parser_OUTPUTS: ${BISON_gfx_color_parser_OUTPUTS}")
# message(STATUS "FLEX_gfx_color_scanner_OUTPUTS: ${FLEX_gfx_color_scanner_OUTPUTS}")
list(APPEND GFX_SRCS ${BISON_gfx_color_parser_OUTPUTS})
list(APPEND GFX_SRCS ${FLEX_gfx_color_scanner_OUTPUTS})
# message("GFX_SRCS ${GFX_SRCS}")

#####

MCWRAPGEN_CLASS(GFX_SRCS ${GFX_MCWG_QIFS})
MCWRAPGEN_MODULE(GFX_SRCS ${GFX_MODLDR_SRC} ${GFX_MCWG_QIFS})

if (WIN32)
  add_definitions(-DGFX_EXPORTS=1)
endif()

add_library(gfx SHARED ${GFX_SRCS})
target_link_libraries(gfx PRIVATE qlib ${LCMS2_LIBRARIES})

add_custom_target(gfx_generate_headers DEPENDS ${MCWG_HEADERS})
add_dependencies(gfx gfx_generate_headers)
