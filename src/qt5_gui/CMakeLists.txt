include_directories(
  ${CMAKE_SOURCE_DIR}/src
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_BINARY_DIR}/src
  ${Boost_INCLUDE_DIRS}
  )

# Find includes in the build directories
set(CMAKE_INCLUDE_CURRENT_DIR ON)

# Turn on automatic invocation of the MOC, UIC & RCC
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTORCC ON)

# Find the QtWidgets library
find_package(Qt5 REQUIRED COMPONENTS Core Widgets OpenGL)
message(STATUS "Qt5Widgets_FOUND: ${Qt5Widgets_FOUND}")

# Default config path
file(TO_NATIVE_PATH "${CMAKE_INSTALL_PREFIX}/data/sysconfig.xml" DEFAULT_SYSCONFIG_PATH)
STRING(REPLACE "\\" "\\\\" DEFAULT_SYSCONFIG_PATH ${DEFAULT_SYSCONFIG_PATH})
message("DEFAULT_SYSCONFIG_PATH: ${DEFAULT_SYSCONFIG_PATH}")
add_definitions(-DDEFAULT_CONFIG=\"${DEFAULT_SYSCONFIG_PATH}\")

SET(QT5GUI_SRCS
  # main.cpp
  # mainwindow.cpp
  qt5_gui.cpp
  QtGlDisplayContext.cpp
  QtGlView.cpp
  QtMolWidget.cpp
  # QtTimerImpl.cpp
  # QtScenePanel.cpp
  # QtMolStructPanel.cpp
  # QtTextRender.cpp
  # QtNewSceneCommand.cpp
  # QtLoadSceneCommand.cpp
  # QtLoadObjectCommand.cpp
  # QtGUIManager.cpp
  # QtCreateRendDlg.cpp
  )

#####

# CueMol interface definitions for MCWRAPGEN
SET(QT5GUI_MCWG_QIFS
QtNewSceneCommand.qif
QtLoadSceneCommand.qif
QtLoadObjectCommand.qif
QtGUIManager.qif)

# module loader generated by MCWG
SET(QT5GUI_MODLDR_SRC qt5gui.moddef)

# MCWRAPGEN_CLASS(QT5GUI_SRCS ${QT5GUI_MCWG_QIFS})
# MCWRAPGEN_MODULE(QT5GUI_SRCS ${QT5GUI_MODLDR_SRC} ${QT5GUI_MCWG_QIFS})

##################################################

SET(QT5GUI_LINK_LIBRARIES
  # pcre
  # expat
  # qmpng
  # qmzlib
  qlib
  gfx
  qsys
  sysdep
  # molstr
  # molvis
  # surface
  # molanl
  # symm
  # xtal
  # lwview
  # render
  # anim
  # mdtools
  # importers
  # pybr
  Boost::filesystem
  CGAL::CGAL
  Qt5::Widgets
  Qt5::OpenGL
  )

# Add the Qt5 Widgets for linking
# target_link_libraries(cuemol2 ${QT5GUI_LINK_LIBRARIES})

# MCWRAPGEN_PYWRAPPERS(cuemol2)

# install(TARGETS cuemol2
#   RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
#   LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
#   )

# ================================== Shiboken detection ======================================

SET(PYSIDE2_DIR "${Python3_SITELIB}/PySide2")
FILE(TO_NATIVE_PATH "${PYSIDE2_DIR}/examples/utils/pyside2_config.py" PYSIDE2_CONFIG)
MESSAGE(STATUS "PYSIDE2_CONFIG: ${Python3_EXECUTABLE} ${PYSIDE2_CONFIG}")

# Macro to get various pyside / python include / link flags and paths.
# Uses the not entirely supported utils/pyside2_config.py file.
macro(pyside2_config option output_var)
    if(${ARGC} GREATER 2)
        set(is_list ${ARGV2})
    else()
        set(is_list "")
    endif()

    #      COMMAND ${python_interpreter} "${CMAKE_SOURCE_DIR}/pyside2_config.py"
    execute_process(
      COMMAND ${Python3_EXECUTABLE} ${PYSIDE2_CONFIG}
              ${option}
      OUTPUT_VARIABLE ${output_var}
      OUTPUT_STRIP_TRAILING_WHITESPACE)

    if ("${${output_var}}" STREQUAL "")
        message(FATAL_ERROR "Error: Calling pyside2_config.py ${option} returned no output.")
    endif()
    if(is_list)
        string (REPLACE " " ";" ${output_var} "${${output_var}}")
    endif()
    MESSAGE(STATUS "PYSIDE2_CONFIG OK: ${output_var} ${${output_var}}")
endmacro()

# Query for the shiboken generator path, Python path, include paths and linker flags.
# pyside2_config(--shiboken2-module-path shiboken2_module_path)
pyside2_config(--shiboken2-generator-path shiboken2_generator_path)
# pyside2_config(--python-include-path python_include_dir)
pyside2_config(--shiboken2-generator-include-path shiboken_include_dir 1)
pyside2_config(--shiboken2-module-shared-libraries-cmake shiboken_shared_libraries 0)
# pyside2_config(--python-link-flags-cmake python_linking_data 0)
pyside2_config(--pyside2-shared-libraries-cmake pyside2_link 0)

set(shiboken_path "${shiboken2_generator_path}/shiboken2${CMAKE_EXECUTABLE_SUFFIX}")
if(NOT EXISTS ${shiboken_path})
    message(FATAL_ERROR "Shiboken executable not found at path: ${shiboken_path}")
endif()

MESSAGE(STATUS "shiboken path: ${shiboken_path}")

# ====================== Shiboken target for generating binding C++ files  ====================

list(GET Qt5Core_INCLUDE_DIRS 0 QT_INCLUDE_DIR)
message(STATUS "*** QT_INCLUDE_DIR= ${QT_INCLUDE_DIR}")
get_target_property(QtCore_is_framework Qt5::Core FRAMEWORK)
if (QtCore_is_framework)
    # Get the path to the framework dir.
    get_filename_component(QT_FRAMEWORK_INCLUDE_DIR "${QT_INCLUDE_DIR}/../" ABSOLUTE)
    message(STATUS "*** QT_FRAMEWORK_INCLUDE_DIR is ${QT_FRAMEWORK_INCLUDE_DIR}")

    # QT_INCLUDE_DIR points to the QtCore.framework directory, so we need to adjust this to point
    # to the actual include directory, which has include files for non-framework parts of Qt.
    get_filename_component(QT_INCLUDE_DIR "${QT_INCLUDE_DIR}/../../include" ABSOLUTE)
endif()

message(STATUS "*** computed QT_INCLUDE_DIR as ${QT_INCLUDE_DIR}")

set(shiboken_framework_include_dirs_option "")
if (CMAKE_HOST_APPLE)
  set(shiboken_framework_include_dirs "${QT_FRAMEWORK_INCLUDE_DIR}")
  # make_path(shiboken_framework_include_dirs ${shiboken_framework_include_dirs})
  set(shiboken_framework_include_dirs_option "--framework-include-paths=${shiboken_framework_include_dirs}")
  message(STATUS "*** shiboken_framework_include_dirs_option= ${shiboken_framework_include_dirs_option}")
endif ()

##########

string(REPLACE ";" ":" core_includes "${Qt5Core_INCLUDE_DIRS}")
set(shiboken_include_dirs ${QT_INCLUDE_DIR}:${core_includes})
# get_target_property(qtcore_lib_includes Qt5::OpenGL INTERFACE_INCLUDE_DIRECTORIES)
# MESSAGE(STATUS "qtcore_lib_includes: ${qtcore_lib_includes}")
# list(JOIN qtcore_lib_includes ";-I" lib_includes)
# set(lib_includes "-I${lib_includes}")


# Set up the options to pass to shiboken.
set(shiboken_options --generator-set=shiboken --enable-parent-ctor-heuristic
    --enable-return-value-heuristic --use-isnull-as-nb_nonzero
    --avoid-protected-hack
    --enable-pyside-extensions
    ${shiboken_framework_include_dirs_option}
    --include-paths=${shiboken_include_dirs}
    # ${lib_includes}
    -I${CMAKE_CURRENT_SOURCE_DIR}
    -T${CMAKE_CURRENT_SOURCE_DIR}
    -T${PYSIDE2_DIR}/typesystems/
    --output-directory=${CMAKE_CURRENT_BINARY_DIR}
    )

set(wrapped_header ${CMAKE_CURRENT_SOURCE_DIR}/bindings.h)
set(typesystem_file ${CMAKE_CURRENT_SOURCE_DIR}/bindings.xml)
set(generated_sources
  ${CMAKE_CURRENT_BINARY_DIR}/qt5gui/qtmolwidget_wrapper.cpp
  ${CMAKE_CURRENT_BINARY_DIR}/qt5gui/qt5gui_module_wrapper.cpp)


set(generated_sources_dependencies ${wrapped_header} ${typesystem_file})

# Add custom target to run shiboken to generate the binding cpp files.
add_custom_command(OUTPUT ${generated_sources}
  COMMAND ${shiboken_path}
  ${shiboken_options} ${wrapped_header} ${typesystem_file}
  DEPENDS ${generated_sources_dependencies}
  IMPLICIT_DEPENDS CXX ${wrapped_header}
  WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  COMMENT "Running generator for ${typesystem_file}.")


# =============================== CMake target - qt5gui ===============================

Python3_add_library(qt5gui MODULE ${QT5GUI_SRCS} ${MCWG_HEADERS} ${generated_sources})
# MCWRAPGEN_PYWRAPPERS(qt5gui)
target_link_libraries(qt5gui PRIVATE ${QT5GUI_LINK_LIBRARIES} ${shiboken_shared_libraries} ${pyside2_link})

target_include_directories(qt5gui PRIVATE ${PYSIDE2_DIR}/include/)
target_include_directories(qt5gui PRIVATE ${PYSIDE2_DIR}/include/QtCore)
target_include_directories(qt5gui PRIVATE ${PYSIDE2_DIR}/include/QtWidgets)
target_include_directories(qt5gui PRIVATE ${PYSIDE2_DIR}/include/QtGui)
target_include_directories(qt5gui PRIVATE ${PYSIDE2_DIR}/include/QtOpenGL)
target_include_directories(qt5gui PRIVATE ${shiboken_include_dir})

if (WIN32)
  set_target_properties(qt5gui PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
endif ()

# set_property(TARGET qt5gui PROPERTY PREFIX "")
# Needed mostly on Windows to export symbols, and create a .lib file, otherwise the binding
# library can't link to the sample library.
# target_compile_definitions(qt5gui PRIVATE BINDINGS_BUILD)

# For VS Debug
set_target_properties(qt5gui PROPERTIES
  VS_DEBUGGER_COMMAND "${Python3_EXECUTABLE}"
  VS_DEBUGGER_COMMAND_ARGUMENTS "${CMAKE_SOURCE_DIR}/src/python/cuemol_gui/startup.py"
  VS_DEBUGGER_WORKING_DIRECTORY "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${CMAKE_CFG_INTDIR}"
  VS_DEBUGGER_ENVIRONMENT "PATH=${BOOST_LIBRARYDIR};${CMAKE_PREFIX_PATH}/bin;%PATH%
PYTHONPATH=${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${CMAKE_CFG_INTDIR};${CMAKE_SOURCE_DIR}/src/python;${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/python
CUEMOL_SYSCONFIG_PATH=${CMAKE_SOURCE_DIR}/src/xul_gui"
  )
